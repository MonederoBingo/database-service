import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'org.flywaydb:flyway-gradle-plugin:4.2.0'
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.flywaydb.flyway'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	mavenLocal()
}

ext {
	springCloudVersion = 'Dalston.SR1'
}

configurations {
	deployment
}

dependencies {
	compile 'org.springframework:spring-jdbc:3.1.0.RELEASE'
	compile 'postgresql:postgresql:9.1-901-1.jdbc4'
	deployment 'postgresql:postgresql:9.1-901-1.jdbc4'
	compile 'net.sourceforge.collections:collections-generic:4.01'
	compile 'org.json:json:20090211'
	compile 'commons-lang:commons-lang:2.6'
	compile 'org.springframework.cloud:spring-cloud-starter-config'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile 'com.monederobingo:libs:0.0.8'
	compile 'xyz.greatapp:libs-database:0.0.11'
	compile 'xyz.greatapp:libs:0.0.22'
	compile 'org.flywaydb:flyway-core:3.0'
	deployment 'org.flywaydb:flyway-core:3.0'
	testCompile 'org.mockito:mockito-core:2.8.9'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile group: 'com.h2database', name: 'h2', version: '1.3.148'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	exclude '**/*IntegrationTest*'
}

task migrateMonedero(type: FlywayMigrateTask) {
	extension = new FlywayExtension()
	extension.schemas = ["monedero"]
	extension.url = 'jdbc:postgresql://localhost:5432'
	extension.user = 'postgres'
	extension.password = 'root'
	extension.locations = ["filesystem:${projectDir}/migrations/common",
				 "filesystem:${projectDir}/migrations/postgresql"]
	extension.sqlMigrationPrefix = ""
	extension.baselineOnMigrate = true
	extension.outOfOrder = true
}

task migrateMonederoTest(type: FlywayMigrateTask) {
	extension = new FlywayExtension()
	extension.schemas = ["monedero_test"]
	extension.url = 'jdbc:postgresql://localhost:5432'
	extension.user = 'postgres'
	extension.password = 'root'
	extension.locations = ["filesystem:${projectDir}/migrations/common",
						   "filesystem:${projectDir}/migrations/postgresql"]
	extension.sqlMigrationPrefix = ""
	extension.baselineOnMigrate = true
	extension.outOfOrder = true
}

task migrate
migrate.dependsOn migrateMonedero
migrate.dependsOn migrateMonederoTest


task copyDeploymentJars(type: Copy) {
	from configurations.deployment
	into "$buildDir/dependencies"
}

build.dependsOn copyDeploymentJars

task stage(type: Copy, dependsOn: [clean, build]) {
	from jar.archivePath
	into project.rootDir
	rename {
		'app.jar'
	}
}
stage.mustRunAfter(clean)

clean {
	doLast {
		project.file('app.jar').delete()
	}
}
